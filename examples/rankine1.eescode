"VL050325
Répétition 7, exercice 1

1.	Dans une centrale à vapeur de 60 MW, la vapeur entre dans la turbine à une pression de 70 bar et une température de 500°C. 

Sachant que :
*	La pression au condenseur à eau est de 0.1 bar,
*	Les pertes de charge dans la chaudière sont estimées à environ 4 bar,
*	Le rendement global de la chaudière est de 0.90,
*	L'augmentation de la température d'eau au condenseur est limitée à 10°C,
*	L'efficacité isentropique de la turbine est égale à 0.85,
*	Le rendement de l'alternateur est égal à 0.95,
*	L'efficacité isentropique des pompes et le rendement des moteurs électriques d'entraînement sont égaux à 0.85 et 0.94 respectivement,

On demande de calculer :
*	Le débit de vapeur dans la machine,
*	La puissance de la chaudière,
*	Le rendement de la machine,
*	Le débit d'eau au condenseur.

"

W_dot_el=60E6 "[W]"
p_cd=0.1E5 "[Pa]"
DELTA_p=4E5 "[Pa]"
eta_boiler=0.9
DELTAT_w_cd=10
epsilon_s_t=0.85
eta_a=0.95
epsilon_s_p=0.85
eta_m=0.94

"POINT 1: sortie du condenseur:"
x_1=0
h_1=enthalpy(water,P=P_cd,x=x_1)
s_1=entropy(water,P=P_cd,x=x_1)

"POINT 2: sortie de la pompe"
epsilon_s_p=(h_2_s-h_1)/(h_2-h_1)
h_2_s=enthalpy(water,s=s_1,P=P_2)
P_2=P_3+DELTA_P
P_3=70E5

"POINT 3: sortie de la chaudière= entrée de la turbine"
T_3=500 "[°C]"
h_3=enthalpy(water,P=P_3,T=T_3)
s_3=entropy(water,P=P_3,T=T_3)

"POINT 4: sortie de la turbine"
epsilon_s_t=(h_3-h_4)/(h_3-h_4_s)
h_4_s=enthalpy(water,s=s_3,P=P_cd)
x_4=quality(water,P=P_cd,h=h_4)

"PUISSANCE CENTRALE:"
W_dot_el=W_dot_t*eta_a
W_dot_t=M_dot_steam*(h_3-h_4)

"CHAUDIERE"
Q_dot_boiler=M_dot_steam*(h_3-h_2)/eta_boiler

"TRAVAIL POMPE"
W_dot_p=M_dot_steam*(h_2-h_1)/eta_m

"RENDEMENT"
eta=W_dot_el/(Q_dot_boiler+W_dot_p)

"DEBIT EAU CONDENSEUR"
Q_dot_cd=M_dot_steam*(h_4-h_1)
Q_dot_cd=M_dot_w_cd*cp_w*DELTAT_w_cd
cp_w=4186{$ID$ #1206: Jean Lebrun, Laboratoire de Thermodynamique, Univ. Liege sylvain}{$PX$96}